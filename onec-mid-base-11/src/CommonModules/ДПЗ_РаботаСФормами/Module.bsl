#Область СлужебныйПрограммныйИнтерфейс
Процедура ДополнитьФорму(Форма) Экспорт
	
	// ++ НаумовИВ ___ 2026-01-08
	// Добавление поля "Контактное лицо" в документы конфигурации, 
	//поля для ввода скидки и кнопки пересчета таблицы ЗаказаПокупателя
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьГруппаСкидкиВГруппаШапкаЛево(Форма);
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
	КонецЕсли;
	// -- НаумовИВ ___ 2026-01-08
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДПЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДПЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьГруппаСкидкиВГруппаШапкаЛево(Форма)
	
	ГруппаСкидки = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДПЗ_СогласованнаяСкидка";
	ПолеВвода.Заголовок = "Согласованная скидка:";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "ПересчитатьТаблицу";
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки); 
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Картинка = БиблиотекаКартинок.Перечитать;
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		
КонецПроцедуры 
#КонецОбласти
